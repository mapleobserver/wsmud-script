//~silent
// 挂机自动练习
// 四区白三三
($localVer) = 2020112501
($changeLog) = 修复技能找不到时重复查找功能的判断bug
@js ($version) = $.ajax({type: "GET", url: "http://wsmud-cdn.if404.com/version.txt", async: false, cache: false}).responseText;
//@print (version)
@js ($remoteVer) = var v = (version);v.jqlx
[if] (localVer) != (remoteVer)
  @js WG.SendCmd("tm 当前精确练习版本【(localVer)】，将开始自动更新至最新【(remoteVer)】。")
  ($f_ss)={"name":"精确练习","source":"http://wsmud-cdn.if404.com/提升/精确练习.flow.txt","finder":"提升"}
  @js var time = Date.parse( new Date());var f=(f_ss);var n=f["name"];var s=f["source"];var fd=f["finder"];WorkflowConfig.removeWorkflow({"name":n,"type":"flow","finder":fd});$.get(s,{stamp:time},function(data,status){WorkflowConfig.createWorkflow(n,data,fd);});
  @wait 1000
  ($log) = 精确练习已更新至最新版【(remoteVer)】，即将自动运行。本次更新内容：(changeLog)
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>
  @js var f=(f_ss);ManagedPerformerCenter.start(f["name"], FlowStore.get(f["name"]));
  [exit]
[else]
  ($log) = 开始运行精确练习，当前版本【(remoteVer)】。更新内容：(changeLog)
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>

@toolbar jh
@toolbar skills
//级别上限
@js ($level) = /\d+/.exec($(".obj-money").text())['0']
($skill_base)='基本拳脚','基本内功','基本轻功','基本招架','基本剑法','基本刀法','基本杖法','基本鞭法','基本暗器','基本棍法'
[if] (Skill) == null
    ($Skill) = (skill_base)
[if] (lianxi_map) == null
    ($lianxi_map) = 住房-练功房
@js ($thisroom) = '(:room)'.indexOf('副本区域')
[if] (thisroom) != -1
    ($lianxi_map) = 原地不动
    ($LXAfter) = stopstate;$tts 练习结束，请手动进行后续操作;$wait 5000;$music
[if] (Chiyao) == null
    ($Chiyao) = 否
[if] (Feiyi) == null
    ($Feiyi) = 否
[if] (LXAfter) == null
    ($LXAfter) = $zdwk
#input ($Skill)=技能列表(请用英文单引号和逗号隔开要练习的技能),(Skill)
#input ($target_level)=目标技能等级,(level)
#input ($LXBefore)=练习前执行命令(用英文;隔开),(LXBefore)
#select ($lianxi_map)=练习地点,住房-练功房|帮会-练功房|原地不动,(lianxi_map)
#select ($Chiyao)=是否吃冰心丹,是|否,(Chiyao)
#select ($Feiyi)=是否补飞翼(请事先装备飞翼剑),是|否,(Feiyi)
#input ($LXAfter)=练习结束后执行命令(用英文;隔开，默认请填$zdwk),(LXAfter)
#config
[if] (arg0) != null
    ($target_level) = (arg0)
[if] (arg1) != null
    ($lianxi_map) = (arg1)
[if] (arg2) != null
    ($Chiyao) = (arg2)
[if] (arg3) != null
    ($Feiyi) = (arg3)
<-stopSSAuto
//获取技能数量
@js ($skill_num) = [(Skill)].length
//传送到练功房
stopstate
[if] (lianxi_map) != 原地不动
    $to (lianxi_map)
//练习前执行命令
[if] (LXBefore) != null
    (LXBefore)
    $wait 5000
//循环判断
($num)=0
($endless)=0
($tip_done) = start
[while](num) < (skill_num)
    [if] (tip_done) != null || (tip_limit) != null || (tip_base) != null || (endless) > 0
        ($tip_done) = null
        ($tip_limit) = null
        ($tip_base) = null
        //依次获取技能列表
        @toolbar jh
        @await 200
        @toolbar skills
        @js ($skill_name) = [(Skill)][(num)]
        //获取技能id
        @js ($skilltype) = '(skill_name)'.indexOf('基本')
        [if] (skilltype) != -1
            @js ($skill_id) = $(".base:contains((skill_name))").attr("skid")
        [else]
            [if] (skill_name) == 野兽扑咬
                @js ($skill_id) = $(".base:contains((skill_name))").attr("skid")
            [else]
                @js ($skill_id) = $(".skill:contains((skill_name))").attr("skid")
        @print (skill_id)
        [if] (skill_id) == null
            [if] (endless) = 5
                tm 连续(endless)次无法获取【(skill_name)】技能id，跳过！
                ($endless)=0
                ($num)=(num)+1
            [else]
                @print <ord>未获取到【(skill_name)】技能id，正在重试！</ord>
                ($endless)=(endless)+1
            [continue]
        stopstate
    //开始练习
    @await 500
    [if] (:room) != (lianxi_map) && (lianxi_map) != 原地不动
        stopstate
        [if] (:room) == 扬州城-矿山
            (LXBefore)
            @await 5000
        $to (lianxi_map)
        @await 500
    //避免@tip对同一句内容重复生效
    lianxi (skill_id) (target_level)
    //判断练习状态
    @tip 缺乏($tip_limit)经验|已经练习($tip_done)了|练习($tip_done)毕|你的($tip_base)功火候未到|等级($levelup)了|提升到($levelup)级|你的($tip_qn)不够
    [if] (levelup) != null
        //判断是否吃药
        [if] (:status food) == false && (Chiyao) == 是 && {b冰心丹}? != null
            stopstate
            use {b冰心丹}?
            @await 1000
        //判断是否用飞翼
        [if] (:status fy) == false && (Feiyi) == 是
            stopstate
            use (:eq0)
            @await 1000
        ($levelup) = null
        [continue]
    [if] (tip_limit) != null
        tm 技能上限不足(target_level)级，(skill_name)无法继续练习。
    [else if] (tip_base) != null
        tm 基础技能等级不足(target_level)级，(skill_name)无法继续练习。
    [else if] (tip_done) != null
        tm (skill_name)已练习(target_level)级，继续练习下一个。
    [else if] (tip_qn) != null
        tm 潜能不够，停止练习！
        [break]
    ($num) = (num) + 1
stopstate
(LXAfter)
stopSSAuto->