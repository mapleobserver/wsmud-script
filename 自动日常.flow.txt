//~silent
//自动日常合并版
//四区白三三
//1.自动师门、买吃养精。2.自动调用自动追捕和自动武道两个流程，可选择副本自动脚本、扫荡或跳过。3.当铺购买、请安。4.日常开始结束语音播报提醒。5.新增春节免费扫荡模式。
($localVer) = 2021022802
($changeLog) = 1.新增【古宗门遗址】单独扫荡；2.副本扫荡也可选【古宗门遗址】。3.武神自动跳过请安。
($repourl) = https://cdn.jsdelivr.net/gh/mapleobserver/wsmud-script
@js ($version) = $.ajax({type: "GET", url: "(repourl)/version.txt", async: false, cache: false}).responseText;
//@print (version)
@js ($remoteVer) = var v = (version);v.zdrc
[if] (localVer) != (remoteVer) && (remoteVer) != null
  @js WG.SendCmd("tm 当前自动日常版本【(localVer)】，将开始自动更新至最新【(remoteVer)】。")
  ($f_ss)={"name":"自动日常","source":"(repourl)/自动日常.flow.txt","finder":"根文件夹"}
  @js var time = Date.parse( new Date());var f=(f_ss);var n=f["name"];var s=f["source"];var fd=f["finder"];WorkflowConfig.removeWorkflow({"name":n,"type":"flow","finder":fd});$.get(s,{stamp:time},function(data,status){WorkflowConfig.createWorkflow(n,data,fd);});
  @wait 1500
  ($log) = 自动日常已更新至最新版【(remoteVer)】，即将自动运行。
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>
  @js var f=(f_ss);ManagedPerformerCenter.start(f["name"], FlowStore.get(f["name"]));
  [exit]
[else]
  [if] (remoteVer) == null
    ($log) = 获取远程版本失败，维持当前版本不变。
  [else]
    ($log) = 开始运行自动日常，当前版本【(remoteVer)】。更新内容：(changeLog)
  @js WG.SendCmd("tm (log)")
  @print <ord>(log)</ord>

// 正月初八（2月19日）自动恢复正常模式
//[if] (:date)>=19 && (:hour)>=5
//  ($RCMode) = 正常模式

//检测状态
@js ($fbroom) = '(:room)'.indexOf('副本区域')
[if] (fbroom) != -1 || (:state)==推演 || (:state)==领悟
  ($alert_start) = 正在(:state)，停止自动日常！
  [(fbroom) != -1]($alert_start) = 正在副本中，停止自动日常！
  @js WG.SendCmd("tm (alarm)")
  @print <ord>(alert_start)</ord>
  [exit]

// 自动日常模式
[if] (RCMode) == null
  ($RCMode) = 正常模式
#select ($RCMode) = 当前模式<ord>(RCMode)</ord>，自动日常的执行模式,正常模式|春节模式,(RCMode)
// 是否统计收益
[if] (RecordG) == null
  ($RecordG) = 是
#select ($RecordG) = 是否显示统计收益,是|否,(RecordG)

// 师门任务
@toolbar jh
@toolbar tasks
@task 师门委托目前完成($smnum)/20个，共连续完成($smComboN)个|师门委托你去寻找($smtype)，共连续完成($smComboN)个
[if] (SMtasks) == null
  ($SMtasks) = 正常进行
[if] (smComboN) >= 100 && (SMtasks) != 无视环数
  [if] (:hour) <5 || (:hour) >= 20
    ($SMtasks) = 正常进行
    #select ($SMtasks) = <ord>处于抢首席时段，师门任务可正常进行</ord>,正常进行|跳过|无视环数,正常进行
  [else]
    ($SMtasks) = 跳过
    #select ($SMtasks) = 师门已连(smComboN)次，<ord>是否先跳过等晚上抢首席</ord>,正常进行|跳过|无视环数,跳过
[else if] (smComboN) < 30 && (SMtasks) != 无视环数
  ($SMtasks) = 正常进行
  #select ($SMtasks) = 师门任务正常进行或跳过(抢首席),正常进行|跳过|无视环数,正常进行
[else]
  #select ($SMtasks) = 师门任务正常进行或跳过(抢首席),正常进行|跳过|无视环数,(SMtasks)

// 追捕自定义参数
[if] (ZByes) == null || (ZByes) == 是
  ($ZByes) = 正常模式
[if] (ZBsx) == null
  ($ZBsx) = 转为正常模式
[if] (ZBWaitCD) == null
  ($ZBWaitCD) = 30
[if] (ZBcdskill) == null
  ($ZBcdskill) = null
[if] (ZBBefore) == null
  ($ZBBefore) = $eq 1
[if] (DieToReset) == null
  ($DieToReset) = 已关闭
#select ($ZByes) = 追捕方式(百姓选跳过),正常模式|快速扫荡|跳过,(ZByes)
#select ($ZBsx) = 快速扫荡达到上限后处理方式,转为正常模式|自动放弃再扫荡,(ZBsx)
#input ($ZBBefore) = 追捕前执行命令(用英文;隔开),(ZBBefore)
#input ($ZBWaitCD) = 从此次追捕开始，等待技能冷却,(ZBWaitCD)
#input ($ZBcdskill) = 需要冷却的技能使用英文逗号隔开或以^开头过滤不需要的，填null全部不等冷却,(ZBcdskill)
#select ($DieToReset) = 死亡自动重置追捕环数,已开启|已关闭,(DieToReset)

// 武道自定义参数
[if] (WudaoMode) == null || (WudaoMode) = 正常肝塔
  ($WudaoMode) = 正常模式
[if] (WudaoWaitCDLevel) == null
  ($WudaoWaitCDLevel) = 30
[if] (WudaoManualMaxLevel) == null
  ($WudaoManualMaxLevel) = 84
[if] (WudaoWaitCDExp) == null
  ($WudaoWaitCDExp) = ^none
[if] (WudaoBefore) == null
  ($WudaoBefore) = $eq 1
#select ($WudaoMode)=武道塔模式<ord>(快速模式建议至少已有血刀九阴)</ord>,正常模式|快速模式|只打塔主,(WudaoMode)
#input ($WudaoBefore)=打塔前执行命令(用英文;隔开),(WudaoBefore)
#input ($WudaoWaitCDExp)=打塔等待以下技能冷却,(WudaoWaitCDExp)
#input ($WudaoWaitCDLevel)=从此层开始，等待技能冷却,(WudaoWaitCDLevel)
#input ($WudaoManualMaxLevel)=从此层开始快速扫荡,(WudaoManualMaxLevel)

// 副本自定义参数
[if] (FBBefore) == null
  ($FBBefore) = $eq 1
[if] (FBnum) == null
  ($FBnum) = 20
[if] (DungeonHpThreshold) == null
  ($DungeonHpThreshold) = 50
[if] (DungeonWaitSkillCD) == null
  ($DungeonWaitSkillCD) = 打开
[if] (DungeonBagCleanWay) == null
  ($DungeonBagCleanWay) = 存仓及售卖
#input ($FBBefore)=副本前执行命令(用英文;隔开),(FBBefore)
#select ($FBName) = 副本(上次所选:<ord>(FBName)</ord>),财主家(简单)|财主家(困难)|丽春院|流氓巷|流氓巷(组队)|兵营|庄府|鳌拜府|天地会|神龙教|神龙教(组队)|关外|温府|五毒教|五毒教(组队)|恒山|青城山|衡山|嵩山|云梦沼泽|云梦沼泽(组队)|桃花岛(简单)|桃花岛(困难)|白驼山|白驼山(组队)|星宿海|冰火岛(简单)|冰火岛(困难)|冰火岛(偷渡)|移花宫(简单)|移花宫(困难)|移花宫(偷渡)|燕子坞(简单)|燕子坞(困难)|燕子坞(偷书)|黑木崖(简单)|黑木崖(困难)|缥缈峰(困难)|缥缈峰(偷渡)|光明顶|光明顶(组队)|光明顶(偷渡)|天龙寺(困难)|天龙寺(偷渡)|血刀门|古墓派(简单)|古墓派(困难)|古墓派(困难偷渡)|华山论剑|侠客岛|净念禅宗(简单)|净念禅宗(困难)|慈航静斋(简单)|慈航静斋(困难)|阴阳谷|战神殿(简单)|战神殿(困难)|古宗门遗址,(FBName)
#input ($FBnum) = <ord>需要完成的次数</ord>,(FBnum)
#select ($FBWay) = 刷本方式（选自动前先确定插件支持）,自动|扫荡|跳过副本,(FBWay)
#select ($DungeonHpThreshold) = 副本内疗伤，当气血低于百分比,100|90|80|70|60|50|40|30|20|10,(DungeonHpThreshold)
#select ($DungeonWaitSkillCD) = Boss战前等待技能冷却,打开|关闭,(DungeonWaitSkillCD)
#select ($DungeonBagCleanWay) = 背包清理方案,不清理|售卖|存仓及售卖,(DungeonBagCleanWay)

// 赌场兑换残页
[if] (Duihuan) == null
  ($Duihuan) = 不兑换
#select ($Duihuan) = <hiz>赌场兑换副本残页物品</hiz>,不兑换|无念禅功|长生诀|慈航剑典|阴阳九转|战神图录|神器碎片,(Duihuan)

// 古宗门遗址单独扫荡
[if] (GmpSD) == null
  ($GmpSD) = 跳过
[if] (GmpSDnum) == null
  ($GmpSDnum) = 10
#select ($GmpSD) = <ord>古宗门遗址单独扫荡</ord>,跳过|扫荡,(GmpSD)
#input ($GmpSDnum) = <ord>古宗门遗址扫荡次数</ord>,(GmpSDnum)

// 请安
[if] (RC_qingan) == null
  ($RC_qingan) = 请安
[if] (:grade) == 武神
  ($RC_qingan) = 跳过
#select ($RC_qingan) = 是否给首席请安,请安|跳过,(RC_qingan)

// 日常结束后动作
[if](RCAfter_action) == null
  ($RCAfter_action) = $zdwk
#input ($RCAfter_action) = 日常结束后命令(用英文;隔开),(RCAfter_action)

// 语音播报
[if] (TTSover) == null
  ($TTSover) = 播报
#select ($TTSover) = 日常结束后语音播报提醒,播报|不播报,(TTSover)

#config

// 获取角色名
[if] (TTSover) == 播报
  ($myName) = (:name)
  [if] (myName) == null
    stopstate
    @await 1000
    @toolbar jh
    @toolbar score
    @js $(`span[for=0]`).click()
    @js ($myName) = $(".dialog-title").text().split(/\s+/).pop()
  $tts 开始(myName)的自动日常。

// 停止，清包
@stopSSAuto
stopstate
@tidyBag
@await 2000

// 师门
[if] (SMtasks) == 正常进行 || (SMtasks) == 无视环数
  $sm
  @tip 你先去休息一下吧
[else]
  ($alert_sm) = 根据设置，已跳过师门任务。
  @js WG.SendCmd("tm (alert_sm)")

// 吃养精丹和潜能丹药
stopstate
@await 500
[if] {b朱果g}? != null
  [if] {b朱果g#} <= 10
    use {b朱果g}[{b朱果g#}]
  [else]
    use {b朱果g}[10]
  @await 2000
[if] {b潜灵果g}? != null
  [if] {b潜灵果g#} <= 10
    use {b潜灵果g}[{b潜灵果g#}]
  [else]
    use {b潜灵果g}[10]
  @await 2000

// 开始统计收益
[if] (RecordG) == 是
  <-recordGains

// 追捕
@toolbar jh
($currentN) = null
[while] (currentN) == null
  @await 500
  @toolbar tasks
  @task 追杀逃犯：目前完成($currentN)/20个，共连续完成($comboN)个|追杀逃犯：($empty)目前完成($currentN)/20个，共连续完成($comboN)个
($zbtemp) = (ZByes)
[if] (RCMode) == 春节模式 && (zbtemp) == 正常模式
  //($zbtemp) = (ZByes)
  ($zbtemp) = 快速扫荡
[if] (currentN) < 20 && (zbtemp) == 快速扫荡
  @print 开始快速追捕
  stopstate
  @tidyBag
  @await 2000
  [while] (:room) != 扬州城-衙门正厅
    $to 扬州城-衙门正厅
    @await 500
  [while] true
    [if] {b扫荡符#}? < 20 || {b扫荡符}? == null
      [if] (RCMode) == 正常模式
        shop 0 20
    ask3 {程药发}
    @tip 无法($speed)完成|目前完成20/20个|已经完成|你的($sdfnum)符不够
    [if] (speed) != null || (sdfnum) != null
      [if] (ZBsx) == 转为正常模式
        ($alert_zb) = 追捕到达上限，无法快速扫荡，转为正常模式！
        ($ZByes) = 正常模式
      [else if] (ZBsx) == 自动放弃再扫荡
        [if] (comboN) < 200
          ($alert_zb) = 追捕到达上限，无法快速扫荡，环数低于200环，不自动放弃，转为正常模式！
          ($ZByes) = 正常模式
        [else if] (sdfnum) != null
          ($alert_zb) = 买不起扫荡符，无法快速扫荡，转为正常模式！
          ($ZByes) = 正常模式
          ($sdfnum) = null
        [else]
          ($alert_zb) = 追捕到达上限，无法快速扫荡，自动放弃再扫荡！
          ask1 {程药发}
          ask2 {程药发}
          ($speed) = null
          @await 1000
          [continue]
      ($zbtemp) = (ZByes)
    [else]
      ($currentN) = 20
    [break]
  [if] (alert_zb) != null
    @print <ord>(alert_zb)</ord>
    tm (alert_zb)
[if] (currentN) < 20 && (zbtemp) == 正常模式
  @print 开始自动追捕
  @cd
  stopstate
  (ZBBefore)
  @await 5000
  @tidyBag
  @await 2000
  @js $('#zb_prog').remove()
  @js $(`.state-bar`).before(`<div id=zb_prog>开始追捕</div>`)
  [while](currentN) < 20
    ($zb) = true
    [while] (:room) != 扬州城-武庙
      $to 扬州城-武庙
      @await 500
    @liaoshang
    @until (:status xuruo) == false
    [if](comboN)>=(ZBWaitCD)
      @cd (ZBcdskill)
    [while] (:room) != 扬州城-衙门正厅
      $to 扬州城-衙门正厅
      @await 500
    ($olddir1) = (dir1)
    ($olddir2) = (dir2)
    //@print (olddir1)
    ($escapee) = null
    [while] (escapee) == null
      ($zbing) = null
      ask1 {程药发}
      @tip 说道：($escapee)作恶多端($info)最近在($dir1)-($dir2)出现|你不是在($zbing)吗|没有在逃的逃犯|你的($party)正在进攻
      [if] (party) != null
        ($party) = null
        @print <ord>帮战未结束，获取逃犯信息失败，重试中。</ord>
        [continue]
      [if] (zbing) != null
        ($zbing) = null
        [if] (dir1) == null || (dir2) == null || (escapee) == null
          @await 1000
          @toolbar jh
          @toolbar tasks
          @await 500
          @task 追杀逃犯：($escapee)，据说最近在($dir1)-($dir2)出现过，你还有($time)去寻找他，目前完成($currentN)/20个，共连续完成($comboN)个。|追杀逃犯：目前完成($currentN)/20个，共连续完成($comboN)个
      @print <hiy>追捕目标：(escapee) 目标地点：(dir1)-(dir2)</hiy>
    [if](olddir1) != (dir1) && (olddir2) != (dir2)
      ($start_h) = (:hour)
      ($start_m) = (:minute)
    @await 500
    [while] {r(escapee)}? == null
      [if] (tb) == null
        ($tb) = 0
      <---
      [if] (zb) == true
        @cmdDelay 1000
        [if] {r(escapee)}? != null
          ($type1) = null
          ($hunmi) = null
          kill {r(escapee)}?
          @until {(escapee)的尸体}? != null || {r(escapee)}? ==  null || (:combating) == false || (:living) == false
          @tip 你的追捕任务($done)了，目前完成($currentN)/20个，已连续完成($comboN)个。|你($type1)死了($type2)|你要攻击谁|追捕任务失败了|你又($hunmi)了知觉
          [if] (done) != null
            ($done) = null
            @js $('#zb_prog').html(`<hiy>当前追捕任务已完成，进度 (currentN)/20</hiy>`)
          //[if] (hunmi) != null
            //($hunmi) = null
          relive
          [if] (type1) != null
            relive
            [if] (DieToReset) == 已开启
              [while] (:room) != 扬州城-衙门正厅
                $to 扬州城-衙门正厅
                @await 500
              ask2 {程药发}
          [break]
        [if] (DieToReset) == 已关闭
          [if] (:hour) == 0 && (start_h) != 0
            ($tb) = 24*60+(:minute)-(start_h)*60-(start_m)
          [else]
            ($tb) = (:hour)*60+(:minute)-(start_h)*60-(start_m)
          @js $('#zb_prog').html(`<hiy>已耗时(tb)分钟</hiy> <ord>追捕目标：(dir1)-(dir2) (escapee)</ord>`)
          //@print <hiy>已耗时(tb)分钟。</hiy><ord>追捕目标：(escapee) 目标地点：(dir1)-(dir2)</ord>
          [if] (tb) >= 10 || (tb) < 0
            @print <hiy>追捕超时！</hiy>
            ($dir1) = null
            ($dir2) = null
            ($tb) = null
            [break]
      --->
      @cmdDelay 1000
      $to (dir1)-(dir2)
      [if] (dir1) == 武当派
        [if](dir2) == 林间小径
          go south
        jh fam 1 start
        go north
        go south;go west
        go west
        go east;go northup
        go north
        go east
        go west;go west
        go northup
        go northup
        go northup
        go north
        go north
        go north
        go north
        go north
        go north
      [else if] (dir1) == 华山派
        jh fam 3 start
        go eastup
        go southup
        jumpdown
        go southup
        go south
        go east
        jh fam 3 start
        go westup
        go north
        go east
        go west;go north
        go east
        go west;go north
        go south[3];go west
        go east;go south
        go southup
        go southup
        look bi;break bi;go enter
        go westup
        go westup
        jumpup
      [else if] (dir1) == 少林派
        [if](dir2) == 竹林
          go south
        jh fam 2 start
        go north
        go west
        go east;go east
        go west;go north
        go northup
        go southdown;go northeast
        go northwest
        go southwest
        go northeast;go north
        go east
        go west;go west
        go east;go north
        go east
        go west;go west
        go east;go north
        go west
        go east;go north
        go north
      [else if] (dir1) == 峨眉派
        [if](dir2) == 走廊
          go north
          go south[2]
          go north;go east[2]
        jh fam 4 start
        go northup
        go east
        go west;go southdown;go west
        go south
        go east
        go east
        go west;go south
        go north;go west;go south
        go north;go west
        go south
        go south
        go north;go north;go west
        go east;go north
        go north
      [else if] (dir1) == 逍遥派
        [if](dir2) == 林间小道
          go west;go north
          go south;go west
          go east;go south
        [else if](dir2) == 木屋
          go south[4]
        [else if](dir2) == 地下石室
          go up
        jh fam 5 start
        go north
        go north
        jh fam 5 start;go east
        go north
        go south;go south
        go south
        jh fam 5 start;go west
        go south
        jh fam 5 start;go south
        go south
        jh fam 5 start;go down
        go down
      [else if] (dir1) == 丐帮
        [if](dir2) == 暗道
          go east
          go east[2]
          go east
        jh fam 6 start
        go down
        go east
        go east
        go east
        go up
        go down;go east
        go east
        go up
($zb) = false
//[if] (RCMode) == 春节模式 && (zbtemp) != null
  //($ZByes) = (zbtemp)
@js $('#zb_prog').remove()
@cmdDelay
@await 2000
@tidyBag
@await 2000

// 武道塔
($currentN) = null
[if] (WudaoMode) == 正常模式 || (WudaoMode) == 快速模式  || (RCMode) == 春节模式
  @toolbar jh
  @toolbar tasks
  @task 武道塔($reset)重置，进度($currentN)/($finalN)，|武道塔($reset)重置，进度($currentN)/($finalN)，
  [if] (reset) == 已 && (currentN) != null && (currentN) == (finalN) && (finalN) != 0
    @print <hiy>当日武道塔已完成。</hiy>
  [else]
    stopstate
    @cd
    @print 开始自动武道塔
    @tidyBag
    @await 2000
    (WudaoBefore)
    @await 5000
    @renew
    [if] (WudaoMode) == 快速模式
      @cmdDelay 500
    jh fam 9 start
    @toolbar jh
    @toolbar tasks
    @task 武道塔可以重置，进度($currentN)/($finalN)，|武道塔已重置，进度($currentN)/($finalN)，
    @print 当前武道塔进度：(currentN)/(finalN)
    @await 2000
    [if] (currentN) == (finalN)
      ask1 {r守门人}
      @tip 从头开始挑战|已经重置
      @await 2000
    [while] (wdlevel) == null
      jh fam 9 start
      @await 500
      @tip 你目前可以直接去挑战第($wdlevel)层
    @print 开始挑战第(wdlevel)层
    [if] (RCMode) == 正常模式
      go enter
    [while] (RCMode) == 正常模式
      [if] (:room) == 武道塔-第一百层 || (:room) == 武道塔-塔顶
        [break]
      [if] (WudaoMode) == 正常模式
        [if] (wdlevel) >= (WudaoManualMaxLevel)
          [break]
        [if] (:hpPer) < 0.7 || (:mpPer) < 0.3
          @renew
          jh fam 9 start;go enter
        [if] (wdlevel) >= (WudaoWaitCDLevel)
          @cd (WudaoWaitCDExp)
      [else if] (WudaoMode) == 快速模式
        [while] (:mpPer) <= 0.1
          dazuo
          @await 5000
        stopstate
        [if] (:hpPer) <= 0.01
          @js var jy='(:room)'.indexOf('第七十');if(jy!=-1){WG.SendCmd('liaoshang;$wait 10000;stopstate')}
          @await 100
          @until (:state) != 疗伤
      kill {r武道塔守护者}?
      [if] (WudaoMode) == 正常模式
        @await 500
      @until (:combating) == false && (:free) == true && (:status faint) == false && (:status miss) == false
      [if] {r武道塔守护者}? != null
        [continue]
      [else]
        ($wdlevel) = (wdlevel) + 1
        go up
    [if] (WudaoMode) == 正常模式 || (RCMode) == 春节模式
      ($num)=0
      jh fam 9 start
      ask2 {r守门人}
      @tip 用不着快速挑战了|不用快速挑战|快速挑战需要($num)张扫荡符
      [if] (num) > 0 && (RCMode) != 春节模式
        [if] {b扫荡符#}? < (num) || {b扫荡符}? == null
          shop 0 (num)
      ask2 {r守门人};ask3 {r守门人}
      @tip 你的扫荡符不够|挑战完成|用不着快速挑战了|不用快速挑战
    @cmdDelay
    @await 2000
    stopstate
    @tidyBag
    //@liaoshang
[else if] (WudaoMode) == 只打塔主
  @print <hiy>已设置只打塔主，跳过武道塔。</hiy>
  tm 已设置只打塔主，跳过武道塔。

// 副本
[if] (FBWay) == 跳过副本
  ($alert_fb) = 跳过副本部分，请自己手动完成想要的副本。
[else]
  stopstate
  @toolbar jh
  @toolbar tasks
  @task 副本：($tag2)>($fb_com)/20
  [if] (fb_com) >=(FBnum)
    @print <hiy>当日副本次数已完成(fb_com)次。</hiy>
  [else]
    ($fb_num) = (FBnum) - (fb_com)
    @js ($fbName) = "(FBName)".replace(/\(|\)/g,"")
    ($fbdata) = "财主家简单":"cr yz/cuifu/caizhu 0","财主家困难":"cr yz/cuifu/caizhu 1","丽春院":"cr yz/lcy/dating 0","流氓巷":"cr yz/lmw/xiangzi1 0","兵营":"cr yz/by/damen 0","庄府":"cr bj/zhuang/xiaolu 0","鳌拜府":"cr bj/ao/damen 0","天地会":"cr bj/tdh/hct 0","神龙教":"cr bj/shenlong/haitan 0","关外":"cr bj/guanwai/damen 0","温府":"cr cd/wen/damen 0","五毒教":"cr cd/wudu/damen 0","恒山":"cr wuyue/hengshan/daziling 0","青城山":"cr wuyue/qingcheng/shanlu 0","衡山":"cr wuyue/henshan/hengyang 0","嵩山":"cr wuyue/songshan/taishi 0","云梦沼泽":"cr cd/yunmeng/senlin 0","桃花岛简单":"cr taohua/haitan 0","桃花岛困难":"cr taohua/haitan 1","白驼山":"cr baituo/damen 0","星宿海":"cr xingxiu/xxh6 0","冰火岛简单":"cr mj/bhd/haibian 1","冰火岛困难":"cr mj/bhd/haibian 1","冰火岛偷渡":"cr mj/bhd/haibian 1","移花宫简单":"cr huashan/yihua/shandao 0","移花宫困难":"cr huashan/yihua/shandao 1","移花宫偷渡":"cr huashan/yihua/shandao 1","燕子坞简单":"cr murong/anbian 0","燕子坞困难":"cr murong/anbian 1","黑木崖简单":"cr heimuya/shangu 0","黑木崖困难":"cr heimuya/shangu 1","缥缈峰困难":"cr lingjiu/shanjiao 1","缥缈峰偷渡":"cr lingjiu/shanjiao 1","光明顶":"cr mj/shanmen 0","光明顶偷渡":"cr mj/shanmen 0","天龙寺困难":"cr tianlong/damen 1","天龙寺偷渡":"cr tianlong/damen 1","血刀门":"cr xuedao/shankou 0","古墓派简单":"cr gumu/gumukou 0","古墓派困难":"cr gumu/gumukou 1","古墓派困难偷渡":"cr gumu/gumukou 1","华山论剑":"cr huashan/lunjian/leitaixia 0","侠客岛":"cr xkd/shimen 0","净念禅宗简单":"cr chanzong/shanmen 0","净念禅宗困难":"cr chanzong/shanmen 1","慈航静斋简单":"cr cihang/shanmen 0","慈航静斋困难":"cr cihang/shanmen 1","阴阳谷":"cr yyg/ya 0","战神殿简单":"cr zsd/damen 0","战神殿困难":"cr zsd/damen 1","古宗门遗址":"cr gmp/shanmen 0"
    @js ($fbcr) = var fb={(fbdata)};fb.(fbName)
    [if] (FBWay) == 扫荡
      //扫荡模式
      [if] (FBName) == 流氓巷(组队) || (FBName) == 神龙教(组队) || (FBName) == 五毒教(组队) || (FBName) == 云梦沼泽(组队) || (FBName) == 白驼山(组队) || (FBName) == 冰火岛(偷渡) || (FBName) == 移花宫(偷渡) || (FBName) == 燕子坞(偷书) || (FBName) == 缥缈峰(偷渡) || (FBName) == 光明顶(组队) || (FBName) == 光明顶(偷渡) || (FBName) == 天龙寺(偷渡) || (FBName) == 古墓派(困难偷渡)
        ($alert_fb) = (FBName)无法扫荡，请改为自动模式！
      [else]
        //team out
        @tidyBag
        @await 3000
        [while] (fb_com) < (FBnum)
          ($fb_num) = (FBnum) - (fb_com)
          [if] {b扫荡符#}? < (fb_num) || {b扫荡符}? == null
            [if] (RCMode) == 正常模式
              shop 0 (fb_num)
          [if] (FBName) == 古宗门遗址
            jh fam 9 start;go enter
            go up
            ggdl {r疯癫的老头}
            go north[3]
            go north[3]
            look shi
            tiao1 shi;tiao1 shi;tiao2 shi
            jumpdown
          (fbcr) (fb_num)
          @tip 扫荡完成|扫荡符($lack)继续扫荡|你的($jingli)不够
          [if] (lack) != null
            ($alert_fb) = 扫荡符不足，无法完成对【(FBName)】副本的(FBnum)次扫荡！
            [break]
          [else if] (jingli) != null
            ($alert_fb) = 精力不足，无法完成对【(FBName)】副本的(FBnum)次扫荡！
            [break]
          @tidyBag
          @await 2000
          @toolbar jh
          @toolbar tasks
          @await 500
          @task 副本：($tag2)>($fb_com)/20
    [else if] (FBWay) == 自动
      //脚本自动模式
      stopstate
      (FBBefore)
      @await 5000
      [if] (FBName) == 冰火岛(偷渡) || (FBName) == 移花宫(偷渡) || (FBName) == 缥缈峰(偷渡) || (FBName) == 光明顶(偷渡) || (FBName) == 天龙寺(偷渡) || (FBName) == 古墓派(困难偷渡)
        ($tdnum) = 0
        [while] (tdnum) < (fb_num)
          (fbcr) 0;cr over
          @await 500
          ($tdnum) = (tdnum) + 1
      [else]
        @js ($fbexist) = if(GetDungeonFlow(`(FBName)`)!=null){true}else{false}
        [if] (fbexist) == true
          [if] (FBName) != 白驼山(组队)
            team out
          @js ManagedPerformerCenter.start(`自动副本-(FBName)`, GetDungeonSource("(FBName)").replace(/#.*\n/g,'($_DungeonHpThreshold)=(DungeonHpThreshold)\n($_DungeonWaitSkillCD)=(DungeonWaitSkillCD)\n($_DungeonBagCleanWay)=(DungeonBagCleanWay)\n($_repeat)=(fb_num)\n'))
          @until (:room) == 住房-练功房 || (:room) == 住房-卧室 || (:room) == 住房-院子 || (:room) == 扬州城-大门
        [else]
          ($alert_fb) = (FBName)没有自动脚本！

// 赌场兑换残页
[if] (Duihuan) != 不兑换
  //($dhdata) = "无念禅功":"0_32","长生诀":"1_32","慈航剑典":"1_33","阴阳九转":"0_34","战神图录":"0_35","神器碎片":"1_35"
  //@js ($dhfb) = var dh={(dhdata)};dh.(Duihuan)
  [if] (Duihuan) == 无念禅功
    ($dhfb) = 0_32
  [else if] (Duihuan) == 长生诀
    ($dhfb) = 1_32
  [else if] (Duihuan) == 慈航剑典
    ($dhfb) = 1_33
  [else if] (Duihuan) == 阴阳九转
    ($dhfb) = 0_34
  [else if] (Duihuan) == 战神图录
    ($dhfb) = 0_35
  [else if] (Duihuan) == 神器碎片
    ($dhfb) = 1_35
  [if] (dhfb) != null
    stopstate
    [while] (:room) != 扬州城-赌场
      $to 扬州城-赌场
      @await 500
    ask3 {r易直非}?
    duihuan (dhfb)
    @tip 你目前共完成($dhtotal)次
    @await 200
    @js ($dhcy) = $(".content-message").find(".item-commands").last().children("span:contains('(Duihuan)')").attr("cmd")
    @js ($dhpl) = $(".content-message").find(".item-commands").last().children("span:contains('(Duihuan)')").text().match(/[0-9]+/)
    [if] (dhcy) != null && (dhpl) != null
      @js ($dhnum) = parseInt((dhtotal)/(dhpl))
    [if] (dhnum) > 0 && (dhnum) != null
      ($num) = 0
      [while] (num) < (dhnum)
        (dhcy)
        @await 500
        ($num) = (num) + 1
      ($dhinfo) = 当前完成次数(dhtotal)，每(dhpl)次兑换1份(Duihuan)，共计兑换(num)页。
    [else]
      ($dhinfo) = 当前完成次数(dhtotal)，无法兑换(Duihuan)。
    [if] (dhinfo) != null
      @print <hic>(dhinfo)</hic>
      tm (dhinfo)

[if] (GmpSD) == 扫荡
  @await 1000
  stopstate
  jh fam 9 start;go enter
  go up
  ggdl {r疯癫的老头}
  go north[3]
  go north[3]
  look shi
  tiao1 shi;tiao1 shi;tiao2 shi
  jumpdown
  [if] {b扫荡符#}? < (GmpSDnum) || {b扫荡符}? == null
    [if] (RCMode) == 正常模式
      shop 0 (GmpSDnum)
  cr gmp/shanmen 0 (GmpSDnum)
  @tip 扫荡完成|扫荡符($gmplack)继续扫荡|你的($gmpjingli)不够
  [if] (gmplack) != null
    ($alert_gmp) = 扫荡符不足，无法完成对【古宗门遗址】副本的(GmpSDnum)次扫荡！
  [else if] (gmpjingli) != null
    ($alert_gmp) = 精力不足，无法完成对【古宗门遗址】副本的(GmpSDnum)次扫荡！

// 当铺购买
stopstate
$tnbuy
@await 3000
@tidyBag

// 请安
greet master
[if] (RC_qingan) == 请安
  @await 2000
  @js WG.oneKeyQA()
  @await 5000
[else]
  [if] (:grade) != 武神
    ($alert_qa) = 根据设置，已跳过请安。

// 领取签到奖励
stopstate
taskover signin;taskover zz1;taskover zz2

// 结束收益统计
[if] (RecordG) == 是
  recordGains->
@recoverSSAuto

// 日常结束后
(RCAfter_action)
@print <hiy>自动日常结束！</hiy>
($bobao) = (myName)，自动日常结束！
($report) = 日常报告
[if] (alert_sm) != null
  @print <ord>(alert_sm)</ord>
  ($bobao) = (bobao)(alert_sm)
  ($report) = (report)⚠️(alert_sm)
[if] (alert_fb) != null
  @print <ord>(alert_fb)</ord>
  ($bobao) = (bobao)(alert_fb)
  ($report) = (report)⚠️(alert_fb)
[if] (alert_gmp) != null
  @print <ord>(alert_gmp)</ord>
  ($bobao) = (bobao)(alert_gmp)
  ($report) = (report)⚠️(alert_gmp)
[if] (alert_qa) != null
  @print <ord>(alert_qa)</ord>
  ($bobao) = (bobao)(alert_qa)
  ($report) = (report)⚠️(alert_qa)
@js $('#report_prog').remove()
[if] (report) != 日常报告
  @js $(`.state-bar`).before($(`<div id=report_prog><ord>(report)</ord></div>`).click(() => $('#report_prog').remove()))
[if] (TTSover) == 播报
  $tts (bobao)